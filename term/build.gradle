apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.termoneplus"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        vectorDrawables.useSupportLibrary = true

        versionCode 350
        versionName '3.5.0'

        externalNativeBuild {
            cmake {
                arguments "-DAPPLICATION_ID:STRING=${applicationId}"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    if (project.hasProperty("signing.properties")) {

        Properties properties = new Properties()
        properties.load(new FileInputStream(file(project.property("signing.properties"))))

        signingConfigs {
            release {
                storeFile = file(properties["store.file"].toString())
                storePassword = properties["store.password"].toString()
                keyAlias = properties["key.alias"].toString()
                keyPassword = properties["key.password"].toString()
            }
        }
        android.buildTypes.release.signingConfig = signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "com.google.android.material:material:1.2.1"
}

task elfcleaner(type: Exec) {
    // only unix
    commandLine 'sh', '-c', 'find build/*/stripped* -name lib*.so -exec elf-cleaner {} \\;'
}

tasks.whenTaskAdded { task ->
    if (task.name.matches('strip.*DebugSymbols')) {
        task.finalizedBy elfcleaner
    }
}
